//////////////////////////////////
//  The Sword of Noober v2.0.0  //
//////////////////////////////////
BACKUP ~tnoober/backup~
//AUTHOR ~Pex657@gmail.com~
SUPPORT ~Spellhold Studios at http://www.shsforums.net/topic/36038-the-sword-of-noober/~

VERSION ~2.1.0~

README ~tnoober/readme/tnoober-readme-%LANGUAGE%.txt~ ~tnoober/readme/tnoober-readme-english.txt~


ALWAYS

	/* Converts tra files to UTF-8 */
	ACTION_DEFINE_ARRAY T_reload BEGIN setup END
	ACTION_DEFINE_ARRAY T_noconvert BEGIN END
	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/tra~
			noconvert_array = T_noconvert
			reload_array = T_reload
	END

	/* Loads in any shared functions library (general macros and functions) */
	INCLUDE ~tnoober/lib/gw_functions.tpa~						// Loads functions library dealing with specific EE items descriptions.
	INCLUDE ~tnoober/lib/gw_convert_inventory_bam_to_ee.tpa~	// Function converting inventory BAMs to EE.

END

LANGUAGE ~English~
         ~english~
         ~tnoober/tra/english/setup.tra~

LANGUAGE ~Francais (traduction : Le Marquis)~
         ~french~
         ~tnoober/tra/french/setup.tra~

LANGUAGE ~Francais (translation by MacksimSl)~
         ~russian~
         ~tnoober/tra/russian/setup.tra~


/* ==================================== *
 *         The Sword of Noober          *
 * ==================================== */

BEGIN @1000
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @1020
DESIGNATED 0 LABEL ~the_sword_of_noober~

/* ------ *
 *  Item  *
 * ------ */
COPY ~tnoober/itm/tson#p.itm~ ~override~
	SAY NAME1 #6646
	SAY NAME2 @1
	SAY UNIDENTIFIED_DESC #6738
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@2" END
BUT_ONLY

/* --------------------------------- *
 *  Copy BAMs or convert them to EE  *
 * --------------------------------- */
ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
	ACTION_BASH_FOR ~tnoober/bam/~ ~^.+\.bam$~ BEGIN
		ACTION_TO_LOWER %BASH_FOR_FILE%
		ACTION_TO_LOWER %BASH_FOR_RES%
		COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
			LPF ~gw_convert_inventory_bam_to_ee~ END
		BUT_ONLY
	END
END ELSE BEGIN
	COPY ~tnoober/bam~ ~override~
END

/* ------------------- *
 *  Extending Scripts  *
 * ------------------- */
EXTEND_BOTTOM ~baldur.bcs~ ~tnoober/baf/baldur.baf~
//USING ~tnoober/English/setup.tra~
ACTION_IF GAME_IS ~tob bgt bg2ee eet~ BEGIN
	EXTEND_BOTTOM ~baldur25.bcs~ ~tnoober/baf/baldur.baf~
END

/* -------- *
 *  Dialog  *
 * -------- */
COMPILE ~tnoober/d/tson#p.d~

COPY_EXISTING ~itemdial.2da~ ~override~
	COUNT_2DA_COLS cols
	COUNT_2DA_ROWS cols rows
	INSERT_2DA_ROW rows cols ~TSON#P        5689   TSON#P~
    PRETTY_PRINT_2DA
BUT_ONLY
